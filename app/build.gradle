apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion version_compileSdk
  buildToolsVersion version_buildTools

  defaultConfig {
    minSdkVersion version_minSdk
    targetSdkVersion version_compileSdk

    flavorDimensions "versionCode"
    multiDexEnabled false

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    buildConfigField "boolean", "IS_TEST_BUILD", "false"
    buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
    buildConfigField "String[]", "DETECTED_ANDROID_LOCALES", "${findUsedAndroidLocales()}"
    buildConfigField "String", "BUILD_DATE", "\"${getBuildDate()}\""
//        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""
    buildConfigField "String", "OPEN_WEATHER_API_KEY", OPEN_WEATHER_API_KEY
    buildConfigField "String", "ACCU_WEATHER_API_KEY", ACCU_WEATHER_API_KEY

    resValue "string", "manifest_package_id", "com.fisma.trinity"
    resValue 'string', 'app_name', "TrinityLauncher"

    vectorDrawables.useSupportLibrary = true
    renderscriptTargetApi 21
    renderscriptSupportModeEnabled true

    applicationId "com.fisma.trinity"
    versionCode 37
    versionName "0.6.1"
  }

  productFlavors {
    flavorDefault {
    }

    flavorGplay {
      buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
    }

    flavorNightly {
      buildConfigField "boolean", "IS_NIGHTLY_BUILD", "true"

      versionCode = Integer.parseInt(new Date().format('yyMMdd'))
      versionName = new Date().format('yyMMdd')
      applicationId "com.fisma.trinity.nightly"
      resValue 'string', 'app_name', "TL Nightly"
    }

    flavorGsantner {
      applicationId "net.gsantner.trinity"
      resValue 'string', 'app_name', "TL GS"
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      applicationIdSuffix ".debug"
    }
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        if (details.requested.group == 'com.android.support') {
          if (details.requested.name != 'multidex' && details.requested.name != 'multidex-instrumentation') {
            details.useVersion "${version_library_appcompat}"
          }
        }
      }
    }
  }

  packagingOptions {
    exclude 'META-INF/LICENSE-LGPL-2.1.txt'
    exclude 'META-INF/LICENSE-LGPL-3.txt'
    exclude 'META-INF/LICENSE-W3C-TEST'
  }

  lintOptions {
    disable 'MissingTranslation'
    disable 'InvalidPackage'
    disable 'ExtraTranslation'
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_plugin_kotlin"
  // Support libraries
  implementation 'androidx.recyclerview:recyclerview:1.0.0'
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.legacy:legacy-support-v13:1.0.0'
  implementation 'androidx.preference:preference:1.1.0'
  implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation "androidx.slice:slice-builders-ktx:1.0.0-alpha6"
  implementation "androidx.slice:slice-view:1.0.0"
  implementation "androidx.core:core-ktx:1.1.0"
  // UI Libs
  implementation 'com.mikepenz:fastadapter:3.2.3@aar'
  implementation 'com.mikepenz:fastadapter-commons:3.0.0@aar'
  implementation 'com.mikepenz:fastadapter-extensions:3.0.0@aar'
  implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
  implementation 'com.afollestad.material-dialogs:core:3.1.1'
  implementation 'com.afollestad.material-dialogs:input:3.1.1'
  implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
  implementation 'cat.ereza:customactivityoncrash:2.2.0'
  implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
  implementation 'in.championswimmer:SimpleFingerGestures_Android_Library:1.2'
  implementation 'com.nononsenseapps:filepicker:4.2.1'
  implementation 'com.github.woxthebox:draglistview:1.6.6'
  implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
    transitive = true
  }

  implementation project(path: ':materialscrollbar')
  implementation 'com.hoko:hoko-blur:1.3.4'


  debugImplementation 'com.facebook.flipper:flipper:0.25.0'
  debugImplementation 'com.facebook.soloader:soloader:0.5.1'
  // use this version until the bug fixed
  // debugImplementation 'com.facebook.flipper:flipper-litho-plugin:0.23.4'
  // debugImplementation 'com.facebook.litho:litho-annotations:0.19.0'

  releaseImplementation 'com.facebook.flipper:flipper-noop:0.25.0'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.robolectric:robolectric:4.3'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  implementation "com.squareup.retrofit2:retrofit:2.3.0"
  implementation "com.squareup.retrofit2:converter-gson:2.3.0"
  implementation "com.google.code.gson:gson:2.8.5"
}

repositories {
  mavenCentral()
  flatDir {
    dirs 'libs'
  }
}
